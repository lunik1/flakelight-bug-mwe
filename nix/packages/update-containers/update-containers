#!/usr/bin/env bb
;; mode: clojure -*-
;; vim: ft=clojure

(ns update-containers
  (:require
   [babashka.process :refer [shell]]
   [clojure.string :as str]))

(defn- exit-on-fail
  [procfn]
  (fn
    [& args]
    (try
      (apply procfn args)
      (catch clojure.lang.ExceptionInfo e
        (prn (ex-data e))
        (-> e ex-data :exit System/exit)))))

(def ^:private try-shell
  (exit-on-fail shell))

(defn- super-user?
  []
  (or (= "root" (System/getProperty "user.name"))
      (= "0" (System/getenv "UID"))))

(def ^:private container-image
  (memoize
   (fn [container]
     (-> (try-shell {:out :string} "podman" "inspect" container "--format" "{{.ImageName}}")
         :out
         str/trim))))

(defn- image-digest
  [image]
  (-> (try-shell {:out :string} "podman" "inspect" image "--format" "{{index .RepoDigests 0}}")))

(defn- pull-image!
  [image]
  (println (format "Pulling %s" image))
  (try-shell "podman" "pull" "-q" image))

(defn- restart-container!
  [container]
  (println (format "Restarting %s" container))
  (try-shell "systemctl" "restart" (format "podman-%s.service" container)))

(defn- containers
  []
  (->> (try-shell
        {:out :string}
        "systemctl" "list-units" "-t" "service" "--full" "--all" "--plain" "--no-legend" "podman-*")
       :out
       str/split-lines
       (map (comp
             second
             first
             #(re-seq #"^podman-(?!prune)(\w+).service$" %)
             first
             #(str/split % #"\s+")))
       (remove nil?)))

(defn update-containers!
  []
  (if (super-user?)
    (let [containers (containers)
          current-digests (->> containers
                               (map (comp #(vector % (image-digest %))
                                          container-image))
                               (into {}))
          images (keys current-digests)]
      (run! pull-image! images)
      (let [new-digests (->> images
                             (map #(vector % (image-digest %)))
                             (into {}))]
        (run!
         restart-container!
         (filter
          #(not= (current-digests (container-image %))
                 (new-digests (container-image %)))
          containers))))
    (do
      (println "Requires super user, aborting.")
      (System/exit 19))))

(defn -main []
  (update-containers!))

(-main)
